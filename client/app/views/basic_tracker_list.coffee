request = require 'lib/request'
ViewCollection = require 'lib/view_collection'
BasicTrackerCollection = require 'collections/basic_trackers'


# View to display a tracker where data are from generated by other Cozy apps
# like email.
module.exports = class BasicTrackerList extends ViewCollection
    id: 'basic-tracker-list'
    itemView: require 'views/basic_tracker_list_item'
    template: require 'views/templates/basic_tracker_list'
    collection: new BasicTrackerCollection()

    subscriptions:
        'basic-tracker:add': 'onAddBasicTracker'

    constructor: ->
        super

    afterRender: ->
        super


    # Load data to display into the graph.
    load: (callback) ->
        @collection.fetch
            success: =>
                @reloadAll ->
                    console.log 'load done'
                    callback?()
            error: =>
                alert 'Cannot load basic trackers'
                callback?()


    # Redraw all charts (useful for resizing or data change).
    redrawAll: ->
        view.drawCharts() for id, view of @views


    # Reload data for each tracker. Perform it one by one.
    reloadAll: (callback) ->
        @$(".tracker .chart").html ''
        @$(".tracker .y-axis").html ''

        trackers = []
        trackers.push view for id, view of @views

        async.eachSeries trackers, (tracker, next) ->
            unless tracker.model.get('metadata').hidden
                tracker.load next
            else
                next()
        , (err) ->
            console.log 'reloadAll done'
            callback() if callback?


    # can be overriden if we want to place the subviews somewhere else
    appendView: (view) ->
        @$collectionEl.append view.el
        if view.model.get('metadata').hidden
            view.$el.addClass 'hidden'


    onAddBasicTracker: (slug) ->
        tracker = @collection.findWhere slug: slug
        view = @views[tracker.cid]
        view.$el.removeClass 'hidden'
        view.load()
        data =
            hidden: false
        request.put "basic-trackers/#{slug}", data, (err) ->

